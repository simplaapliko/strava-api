/*
 * Copyright (C) 2018 Oleg Kan
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'com.github.ben-manes.versions'

buildscript {

  ext.buildConfig = [
      'minSdk'    : 21,
      'compileSdk': 29,
      'targetSdk' : 29,
  ]

  ext.versions = [
      'dagger'  : '2.28.3',
      'kotlin'  : '1.4.0',
      'okHttp'  : '4.8.1',
      'retrofit': '2.9.0',
  ]

  ext.deps = [
      'android'        : [
          'support': [
              'annotations'     : "androidx.annotation:annotation:1.1.0",
              'cardview'        : "androidx.cardview:cardview:1.0.0",
              'constraintLayout': "androidx.constraintlayout:constraintlayout:2.0.0",
              'core'            : "androidx.core:core:1.3.1",
              'material'        : "com.google.android.material:material:1.2.0",
          ],
      ],
      'analytics'      : [
          'crashlytics': "com.crashlytics.sdk.android:crashlytics:2.9.8",
      ],
      'dagger'         : [
          'runtime' : "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
      ],
      'javaxannotation': "javax.annotation:jsr250-api:1.0",
      'kotlin'         : [
          'stdlib': [
              'jdk': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
          ],
      ],
      'gson' : "com.google.code.gson:gson:2.8.6",
      'json' : "org.json:json:20200518",
      'okhttp'         : [
          'client' : "com.squareup.okhttp3:okhttp:${versions.okHttp}",
          'logging': "com.squareup.okhttp3:logging-interceptor:${versions.okHttp}",
      ],
      'reactivex'      : [
          'rxAndroid': "io.reactivex.rxjava2:rxandroid:2.1.1",
          'rxJava2'  : "io.reactivex.rxjava2:rxjava:2.2.19",
      ],
      'retrofit'       : [
          'client'       :    "com.squareup.retrofit2:retrofit:${versions.retrofit}",
          'adapterRxJava':    "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
          'converterGson':   "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
          'converterScalars': "com.squareup.retrofit2:converter-scalars:${versions.retrofit}",
      ],
      'test'           : [
          'mockito': "org.mockito:mockito-core:2.18.3",
          'junit'  : "junit:junit:4.13",
      ],
      'truth'          : "com.google.truth:truth:1.0.1",
  ]

  repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.29.0'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
